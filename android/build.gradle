def getRootProjectProperties(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    // The Android Gradle plugin is only required when opening the android folder stand-alone.
    // This avoids unnecessary downloads and potential conflicts when the library is included as a
    // module dependency in an application project.
    if (project == rootProject) {
        repositories {
            google()
            jcenter()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:3.5.0'
        }
    }
}

apply plugin: 'com.android.library'

android {
  compileSdkVersion getRootProjectProperties("compileSdkVersion", 28)
  buildToolsVersion getRootProjectProperties('buildToolsVersion', '25.0.3')
  defaultConfig {
    minSdkVersion getRootProjectProperties("minSdkVersion", 16)
    targetSdkVersion getRootProjectProperties("targetSdkVersion", 28)
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    abortOnError false
  }
}

repositories {
    google()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
    jcenter()
}

def biometric_version = "1.0.0"

dependencies {
    implementation "com.facebook.react:react-native:${getRootProjectProperties('reactNativeVersion', '+')}"
    implementation "androidx.biometric:biometric:$biometric_version"
}